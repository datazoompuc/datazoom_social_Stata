* VERSION 1.0
VERSION 10.0
SYNCHRONOUS_ONLY

POSITION . . 420 300

DIALOG main, label("PNAD COVID19") tabtitle("Main")
BEGIN
******************************************************************************   
GROUPBOX caixaAnos 20 20 190 220, label("Mês")

   CHECKBOX    ck05 +14 +20 210  ., label("Maio/2020")  option(05)
   CHECKBOX    ck06 @ +24 210    ., label("Junho/2020")  option(06)
   CHECKBOX    ck07 @ +24 210    ., label("Julho/2020")  option(07)
   CHECKBOX    ck08 @ +24 210    ., label("Agosto/2020")  option(08)
   CHECKBOX    ck09 @ +24 210    ., label("Setembro/2020")  option(09)
   CHECKBOX    ck10 @ +24 210    ., label("Outubro/2020")  option(10)
   CHECKBOX    ck11 @ +24 210    ., label("Novembro/2020")  option(11)	
   
GROUPBOX caixaDicionários 210 20 190 220, label("Bases de Dados")

	BUTTON	fldados 	+20 +35 100 ., label("Dados originais...") onpush(script dados) 
	BUTTON	flsalvando  @ +35 100 ., label("Salvando...") onpush(script salvando) 

	
TEXT     credit     5   250 200    ., label("PUC-Rio - Departamento de Economia") left
TEXT     notes     5   270 360    ., label("Nota: reinstale periodicamente o pacote datazoom_pnad_covid") left


END


SCRIPT PREINIT_SCRIPT
	BEGIN
   create STRING saidadata
   create STRING saidasalvando
	END

******************************************************************************

OK ok1,      label("OK")
SUBMIT sub1
CANCEL can1, label("Cancel")
HELP hlp1,   view("help datazoom_pnad_covid")
RESET res1

******************************************************************************

	
SCRIPT dados
BEGIN
	create CHILD datazoom_datafolder AS data
	data.settitle "Dados originais da PNAD COVID19"
	data.setExitString saidadata
END

SCRIPT salvando
BEGIN
	create CHILD datazoom_finalfolder AS dic
	dic.settitle "Novas bases da PNAD COVID19"
	dic.setExitString saidasalvando
	END

PROGRAM command
	BEGIN
		put "datazoom_pnad_covid"
		beginoptions
			/*Parte da syntax: semanas a extrair*/
			put "months("
			option main.ck05 main.ck06 main.ck07 main.ck08 main.ck09 main.ck10 main.ck11
			put ") "
			/*Parte da syntax: base de dados originais*/
			put saidadata " "
			put saidasalvando
		endoptions
	END

* VERSION 1.0
VERSION 10.0
SYNCHRONOUS_ONLY

POSITION . . 365 270

DIALOG main, label("PNAD Covid") tabtitle("Main")
BEGIN
******************************************************************************   
GROUPBOX caixaAnos 5 20 190 220, label("Month")

   CHECKBOX    ck05 +14 +20 210  ., label("May/2020")  option(05)
   CHECKBOX    ck06 @ +24 210    ., label("June/2020")  option(06)
   CHECKBOX    ck07 @ +24 210    ., label("July/2020")  option(07)
   CHECKBOX    ck08 @ +24 210    ., label("August/2020")  option(08)
   CHECKBOX    ck09 @ +24 210    ., label("September/2020")  option(09)
   CHECKBOX    ck10 @ +24 210    ., label("October/2020")  option(10)
   CHECKBOX    ck11 @ +24 210    ., label("November/2020")  option(11)

GROUPBOX caixaDicion√°rios 200 65 160 130, label("Databases")

	BUTTON	fldados 	+30 +35 100 ., label("Original files...") onpush(script dados) 
	BUTTON	flsalvando  @ +35 100 ., label("Saving...") onpush(script salvando) 

	CHECKBOX	eng     5  250 110 ., label("Labels in English") option(english)
	
TEXT     credit     120   250 240    ., label("PUC-Rio - Department of Economics") right


END


SCRIPT PREINIT_SCRIPT
	BEGIN
   create STRING saidadata
   create STRING saidasalvando
	END

******************************************************************************

OK ok1,      label("OK")
SUBMIT sub1
CANCEL can1, label("Cancel")
HELP hlp1,   view("help datazoom_pnad_covid")
RESET res1

******************************************************************************

	
SCRIPT dados
BEGIN
	create CHILD datazoom_datafolder AS data
	data.settitle "Dados originais da PNAD COVID19"
	data.setExitString saidadata
END

SCRIPT salvando
BEGIN
	create CHILD datazoom_finalfolder AS dic
	dic.settitle "Novas bases da PNAD COVID19"
	dic.setExitString saidasalvando
	END

PROGRAM command
	BEGIN
		put "datazoom_pnad_covid"
		beginoptions
			/*Parte da syntax: semanas a extrair*/
			put "months("
			option main.ck05 main.ck06 main.ck07 main.ck08 main.ck09 main.ck10 main.ck11
			put ") "
			/*Parte da syntax: base de dados originais*/
			put saidadata " "
			put saidasalvando
			option main.eng
		endoptions
		stata
	END
